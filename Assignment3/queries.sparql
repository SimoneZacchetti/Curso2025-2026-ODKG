# Q1: Get all the properties that can be applied to instances of the Politician class (<http://dbpedia.org/ontology/Politician>)
SELECT DISTINCT ?p
WHERE {
    ?s a <http://dbpedia.org/ontology/Politician>.
    ?s ?p ?o.
}
ORDER BY ?p


# Q2: Get all the properties, except rdf:type, that can be applied to instances of the Politician class
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?p 
WHERE { 
    ?s a <http://dbpedia.org/ontology/Politician>. 
    ?s ?p ?o.
    FILTER (?p != rdf:type). 
} 
ORDER BY ?p


# Q3: Which different values exist for the properties, except for rdf:type, applicable to the instances of Politician?
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?p ?value
WHERE { 
  ?s a <http://dbpedia.org/ontology/Politician> ;
     ?p ?value .
  FILTER (?p != rdf:type)
}
ORDER BY ?p ?value


# Q4: For each of these applicable properties, except for rdf:type, which different values do they take globally for all those instances?
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?p ?value
WHERE {
    {
        # Get all properties except rdf:type for Politician instances
        SELECT DISTINCT ?p 
        WHERE { 
            ?s a <http://dbpedia.org/ontology/Politician>. 
            ?s ?p ?o.
            FILTER (?p != rdf:type)
        }
    }
?instance ?p ?value
}


#Â Q5: For each of these applicable properties, except for rdf:type, how many distinct values do they take globally for all those instances?
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?property (COUNT(DISTINCT ?value) AS ?numValues)
WHERE {
    ?p a dbo:Politician.
    ?p ?property ?value.
    FILTER (?property != rdf:type)
}
GROUP BY ?property
ORDER BY DESC (?numValues)
